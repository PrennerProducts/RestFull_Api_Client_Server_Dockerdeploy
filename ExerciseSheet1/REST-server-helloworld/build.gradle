plugins {
    id 'java'
    id 'application'  // Für gradle run
}

application {
    // Hier die HaubptKlasse für gradle run
    mainClass = 'REST.server.helloworld.ServerApp'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.jboss.resteasy:resteasy-jaxrs:3.15.3.Final'
    implementation 'org.jboss.resteasy:resteasy-servlet-initializer:3.15.3.Final'
    implementation 'javax.servlet:javax.servlet-api:4.0.1'
    implementation 'org.eclipse.jetty:jetty-servlet:9.3.14.v20161028'
    testImplementation 'junit:junit:4.11'
}

group = 'mci.resteasy.examples.fatjar'
version = '1.0-SNAPSHOT'
description = 'fatjar'
sourceCompatibility = '17'

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes(
                'Class-Path': configurations.runtimeClasspath.collect { it.getName() }.join(' '),
                'Main-Class': 'REST.server.helloworld.ServerApp'
        )
    }
    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

//Creates a Assignment.zip under build/distributions
tasks.register('mciSrcZip', Zip) {
    from projectDir
    include 'src/**/*'
    include '*.gradle'
    include 'gradlew'
    include 'gradlew.bat'
    include 'gradle/**/*'
    archiveFileName = "Assignment.zip"
}

tasks.withType(JavaExec).configureEach {
    if (System.getProperty('DEBUG', 'false') == 'true') {
        jvmArgs '-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=9099'
    }
}

